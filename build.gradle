plugins {
    id "java-library"
    id "idea"
    id 'net.neoforged.gradle.userdev' version '7.0.+'
    id "com.modrinth.minotaur" version "2.+"
    id 'net.darkhax.curseforgegradle' version '1.+'
}

version = "${project.mod_version}"
group = "${project.mod_namespace}"
archivesBaseName = "${project.mod_name}"

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

subsystems {
    parchment {
        minecraftVersion="${parchment_mc_version}"
        mappingsVersion="${parchment_mapping_version}"
    }
}

runs {
    configureEach {
        //systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }
    client {
        workingDirectory project.file('run')
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }
    server {
        workingDirectory project.file('run_server')
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }
    gameTestServer {
        workingDirectory project.file('run_gametest')
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }
    data {
        workingDirectory project.file('run_data')
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

repositories {
    mavenLocal()
    maven {
        url = "https://api.modrinth.com/maven"
    }
    maven {
        url = "https://maven.blamejared.com/"
    }
    maven {
        url = "https://modmaven.dev"
    }
}

dependencies {
    implementation "net.neoforged:neoforge:${project.neoforge_version}"
    implementation "maven.modrinth:justabattery:210.0.0"
    implementation "maven.modrinth:pipez:neoforge-1.21-1.2.12"
    implementation "maven.modrinth:cloth-config:15.0.127+neoforge"
//    implementation "maven.modrinth:powah:5.4.0-beta-neoforge"
//    implementation "mezz.jei:jei-${project.jei_version}"
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            mod_loader: "javafml",
            loader_version: "${project.valid_forge_versions}",
            license: "${project.license}",
            mod_id: "${project.mod_id}",
            mod_name: "${project.mod_name}",
            mod_version: "${project.mod_version}",
            mc_version_range: "${project.mc_version_range}"
    ]
    inputs.properties replaceProperties
    filesMatching(['META-INF/neoforge.mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title": project.mod_name,
                "Specification-Vendor": "canitzp",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"canitzp",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

idea {
    module {
        inheritOutputDirs = true
        downloadSources = true
        downloadJavadoc = true
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "mC1vVReZ"
    versionNumber = "${project.mod_version}"
    versionName = "${project.mod_name} - ${project.mod_version} (for ${project.release_minecraft_versions})"
    versionType = "release"
    uploadFile = jar
    gameVersions = project.release_minecraft_versions.split("/").collect()
    loaders = project.release_mod_loaders.toLowerCase().split("/").collect()
    changelog = String.join("\n", rootProject.file("CHANGELOG.md").text.lines().takeWhile(s -> !s.isEmpty()).toList())
    syncBodyFrom = rootProject.file("README.md").text
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)

//noinspection UnnecessaryQualifiedReference
task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    group = "publishing"
    apiToken = System.getenv('CURSEFORGE_TOKEN')

    def mainFile = upload(1024259, jar)
    mainFile.displayName = "${project.mod_name} - ${project.mod_version} (for ${project.release_minecraft_versions})"
    mainFile.releaseType = "release"
    mainFile.changelogType = 'markdown'
    mainFile.changelog = file("CHANGELOG.md")
    for (final def modloader in "${project.release_mod_loaders}".split("/")) {
        mainFile.addModLoader("${modloader}")
    }
    for (final def java_version in "${project.release_java_versions}".split("/")) {
        mainFile.addJavaVersion("${java_version}")
    }
    for (final def minecraft_version in "${project.release_minecraft_versions}".split("/")) {
        mainFile.addGameVersion("${minecraft_version}")
    }
}